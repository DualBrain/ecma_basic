1 REM W=WORD INDEX
2 REM W$=THE WORD TO GUESS
3 REM A=# ATTEMPTS REMAINING
4 REM A$=THE GUESSED WORD
5 REM S=SUCCESS
10 GOSUB 100
15 REM PRINT "THIS IS THE WORD: ";W$
20 GOSUB 200
30 GOSUB 300
40 IF S=1 THEN 500
50 IF A=0 THEN 600
60 GOTO 20
100 REM INITIALIZE
105 RESTORE
115 READ W
120 LET W=RND(W)+1
125 FOR N=1 TO W STEP 1
130 READ W$
135 NEXT N
140 LET A=6
145 LET S=0
150 PRINT "YOU GET";A;"ATTEMPTS TO GUESS THE WORD."
155 PRINT "+ = YOU GOT THE RIGHT LETTER IN THE RIGHT PLACE."
160 PRINT "- = YOU GOT THE RIGHT LETTER, BUT IN THE WRONG PLACE."
165 RETURN
200 REM PLAYER INPUT
210 PRINT "ATTEMPTS REMAINING:";A
220 PRINT "WHAT IS YOUR GUESS";
230 INPUT A$
240 RETURN
300 REM TEST FOR SUCCESS
301 REM FIRST LOOP CHECKS FOR VALID CHARACTERS
302 REM SECOND LOOP TESTS FOR CORRECT PLACEMENT
305 LET S=1
310 FOR N=1 TO 5
315 IF POS(W$,MID$(A$,N,1))<1 THEN 330
320 PRINT MID$(A$,N,1);
325 GOTO 335
330 PRINT "_";
335 NEXT N
340 PRINT
350 FOR N=1 TO 5
355 LET P=POS(W$,MID$(A$,N,1))
360 IF P=0 THEN 390
365 IF P<>N THEN 380
370 PRINT "+";
375 GOTO 395
380 PRINT "-";
381 LET S=0
385 GOTO 395
390 PRINT " ";
391 LET S=0
395 NEXT N
400 PRINT
405 LET A=A-1
410 RETURN
500 REM YOU WIN!
510 PRINT "YOU GUESSED IT!"
520 GOTO 9999
600 REM YOU LOSE.
610 PRINT "ALL OUT OF TURNS."
620 PRINT "THIS IS THE WORD: ";W$
630 GOTO 9999
9000 REM FIRST DATA PIECE IS THE SIZE OF THE DICTIONARY
9001 DATA 6
9002 DATA TIGER,WHILE,JUMPY,CRAZY,JELLY,READY
9999 END
